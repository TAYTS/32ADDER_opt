.include "/Users/taytzushieh/Dropbox/SUTD/SCHOOL_STUFF/TERM4/50-002_Computation_Structures/Software_Lab/50002/nominal.jsim"
.include "/Users/taytzushieh/Dropbox/SUTD/SCHOOL_STUFF/TERM4/50-002_Computation_Structures/Software_Lab/50002/stdcell.jsim"
.include "/Users/taytzushieh/Dropbox/SUTD/SCHOOL_STUFF/TERM4/2D/checkoff2d/2dcheckoff_100ns.jsim"


* 3-input ADD3: inputs are A, b and C, output is S and C
.subckt FA a b ci s co
Xnand1 a b 1 nand2
Xnand2 a ci 2 nand2
Xnand3 b ci 3 nand2
Xnand4 1 2 3 co nand3
Xor1 a b 4 xor2
Xor2 4 ci s xor2
.ends


.subckt NOR4_SUB inp[3:0] z
Xnor4 inp[3] inp[2] inp[1] inp[0] z nor4
.ends


* Compute the Z (All zero)
.subckt Z inp[31:0] z
Xnor0 inp[3:0]   1 NOR4_SUB
Xnor1 inp[7:4]   2 NOR4_SUB
Xnor2 inp[11:8]  3 NOR4_SUB
Xnor3 inp[15:12] 4 NOR4_SUB
Xnor4 inp[19:16] 5 NOR4_SUB
Xnor5 inp[23:20] 6 NOR4_SUB
Xnor6 inp[27:24] 7 NOR4_SUB
Xnor7 inp[31:28] 8 NOR4_SUB
Xand0 1 2 3 4 F1 and4
Xand1 5 6 7 8 F2 and4
Xand2 F1 F2 z and2
.ends


* Compute the V (Overflow)
.subckt V XA XB S v
Xinvs S  inv_s inverter
Xinva XA inv_a inverter
Xinvb XB inv_b inverter
Xand0 XA XB inv_s a0 and3
Xand1 inv_a inv_b S a1 and3
Xor0 a0 a1 v or2
.ends

.subckt adder32 op0 A[31:0] B[31:0] s[31:0] z v n
// output s
Xor0 B[31:0] op0#32 XB[31:0] xor2
Xadd0 A0 XB0 op0 s[0] c[0] FA
Xadd1 A[30:1] XB[30:1] c[29:0] s[30:1] c[30:1] FA
Xadd2 A[31] XB[31] c[30] s[31] co FA

// output z
Xz s[31:0] z Z

// output v
Xv A[31] XB[31] s[31] v V

// output n
.connect n s[31]
.ends